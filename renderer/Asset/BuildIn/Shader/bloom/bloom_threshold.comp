#version 460
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_samplerless_texture_functions : enable

#include "../common/screen.glsl"

layout(set = 0, binding = 0) uniform texture2D IN_COLOR[];
layout(set = 0, binding = 1, rgba16f) uniform image2D OUT_COLOR;
layout(set = 1, binding = 0) uniform sampler SAMPLER[];

layout(push_constant) uniform bloom_setting {
	int maxMip;
	int mipLevel;
	float stride;
	float bias;
	float accumulateIntencity;
	float combineIntencity;
	float thresholdMin;
	float thresholdMax;
	uint bloomOnly;
} BLOOM_SETTING;

float RGBtoLuminance(vec3 c)
{
    return dot(c, vec3(0.2125, 0.7154, 0.0721)); 
}

#define THREAD_SIZE_X 16
#define THREAD_SIZE_Y 16
#define THREAD_SIZE_Z 1
layout (local_size_x = THREAD_SIZE_X, 
		local_size_y = THREAD_SIZE_Y, 
		local_size_z = THREAD_SIZE_Z) in;
void main()
{
    ivec2 pixel       = ivec2(gl_GlobalInvocationID.xy);
    vec2 uv           = ScreenPixToUV(pixel, textureSize(IN_COLOR[0], 0));

	vec4 color        = texture(sampler2D(IN_COLOR[0], SAMPLER[0]), uv);
    vec4 outColor     = color;

	if(RGBtoLuminance(outColor.xyz) < BLOOM_SETTING.thresholdMin) 	outColor = vec4(0.0f);
	if(RGBtoLuminance(outColor.xyz) > BLOOM_SETTING.thresholdMax) 	outColor = outColor * BLOOM_SETTING.thresholdMax / RGBtoLuminance(outColor.xyz);
	if(color.r < 0 || color.g < 0 || color.b < 0) 				outColor = vec4(0.0f);
	
    imageStore(OUT_COLOR, pixel, outColor);
}